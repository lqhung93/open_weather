plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-allopen'
}

def appIdPropertiesFile = rootProject.file("app-id.properties")
def appIdProperties = new Properties()
appIdProperties.load(new FileInputStream(appIdPropertiesFile))

allOpen {
    // allows mocking for classes w/o directly opening them for release builds
    annotation 'com.hung.openweather.utils.testing.OpenClass'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.hung.openweather"
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            buildConfigField("String", "APP_ID", appIdProperties['APP_ID'])
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("String", "APP_ID", appIdProperties['APP_ID'])
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        dataBinding true
    }
}

ext {
    nav_version = "2.4.0"
    retrofit_version = "2.9.0"
    okhttp_version = "3.12.12"
    power_mock_version = "2.0.7"
    espresso_version = "3.4.0"
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'

    // Fragment Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Network
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation 'com.google.code.gson:gson:2.8.7'

    // OkHttp
    implementation('com.squareup.okhttp3:okhttp') {
        version { strictly "$okhttp_version" }
    }
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"

    // Rx Java
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "io.reactivex.rxjava2:rxjava:2.2.19"

    // Unit test dependencies
    testImplementation 'junit:junit:4.+'
    testImplementation 'org.json:json:20180813'
    testImplementation 'org.mockito:mockito-core:2.25.0'
    testImplementation "org.powermock:powermock-module-junit4:$power_mock_version"
    testImplementation "org.powermock:powermock-module-junit4-rule:$power_mock_version"
    testImplementation "org.powermock:powermock-api-mockito2:$power_mock_version"
    testImplementation "org.powermock:powermock-classloading-xstream:$power_mock_version"
    testImplementation "androidx.arch.core:core-testing:2.1.0"

    // Android test dependencies
    androidTestImplementation 'org.mockito:mockito-android:2.25.0'
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espresso_version"
    debugImplementation 'androidx.fragment:fragment-testing:1.4.1'
}